open import 1Lab.Path.Reasoning
open import 1Lab.Prelude

open import Algebra.Monoid.Category
open import Algebra.Group

open import Data.List hiding (lookup)
open import Data.Fin
open import Data.Fin.Closure

open is-iso

{-
This is a formalisation of a hat puzzle whose statement you can read at any
of these places:

  https://www.jsoftware.com/pipermail/general/2007-June/030272.html
  https://www.cut-the-knot.org/blue/PuzzleWithHats.shtml
  https://twitter.com/gro_tsen/status/1618989823100096512

ğŸ© SPOILERS AHEAD! ğŸ©
-}
module Hats where

-- We assume there's at least one person, and that everyone has a unique
-- "name" between 0 and n - 1, known to everyone else.
module _ (n-1 : Nat) where
  private
    n = suc n-1
    Person = Fin n
    Hat = Fin n
    Hats = Person â†’ Hat

  record Strategy : Type where
    -- `guess i xs` is the guess made by the ith person upon seeing the n - 1
    -- other hats given by `xs` (a vector of numbers from 0 to n - 1).
    field
      guess : Person â†’ (Fin n-1 â†’ Hat) â†’ Hat

    -- The relation `hats âœ“ i` means that the ith person guesses correctly,
    -- given the assignment `hats`.
    _âœ“_ : Hats â†’ Person â†’ Type
    hats âœ“ i = guess i (delete hats i) â‰¡ hats i

    -- We are interested in strategies where at least one person guesses
    -- correctly for every assignment of hats.
    field
      one-right : âˆ€ hats â†’ Î£ Person Î» i â†’ hats âœ“ i

    -- First, note that, given this requirement, there can be at *most* one
    -- correct guess for every assignment of hats.
    -- This follows from a probabilistic argument: every person guesses correctly
    -- with probability 1/n, so the total number of correct guesses across all
    -- hat assignments is nâ¿.
    -- In order to conclude, we use the fact that any surjection between finite
    -- sets of equal cardinality is an equivalence.
    exactly-one-right : âˆ€ hats â†’ is-contr (Î£ Person Î» i â†’ hats âœ“ i)
    exactly-one-right hats = Equivâ†’is-hlevel 0 (Fibre-equiv _ hats eâ»Â¹) (p-is-equiv .is-eqv hats)
      where
        probability : âˆ€ i â†’ Iso (Î£ Hats (_âœ“ i)) (Fin n-1 â†’ Hat)
        probability i .fst (hats , _) = delete hats i
        probability i .snd .inv other .fst = other [ i â‰” guess i other ]
        probability i .snd .inv other .snd =
          guess i âŒœ delete (other [ i â‰” guess i other ]) i âŒ â‰¡âŸ¨ ap! (funext (delete-insert _ i _)) âŸ©
          guess i other                                      â‰¡Ë˜âŸ¨ insert-lookup _ i _ âŸ©
          (other [ i â‰” guess i other ]) i                    âˆ
        probability i .snd .rinv _ = funext (delete-insert _ i _)
        probability i .snd .linv (hats , r) = Î£-prop-path! (funext (insert-delete _ i _ (sym r)))

        only-one : Î£ Hats (Î» hats â†’ Î£ Person (hats âœ“_)) â‰ƒ Hats
        only-one =
          Î£ _ (Î» hats â†’ Î£ _ Î» i â†’ hats âœ“ i) â‰ƒâŸ¨ Î£-swapâ‚‚ âŸ©
          Î£ _ (Î» i â†’ Î£ _ Î» hats â†’ hats âœ“ i) â‰ƒâŸ¨ Î£-ap-snd (Isoâ†’Equiv âˆ˜ probability) âŸ©
          Fin n Ã— (Fin n-1 â†’ Fin n)         â‰ƒË˜âŸ¨ Fin-suc-Î  âŸ©
          (Fin n â†’ Fin n)                   â‰ƒâˆ

        p : Î£ Hats (Î» hats â†’ Î£ Person (hats âœ“_)) â†’ Hats
        p = fst
        p-is-equiv : is-equiv p
        p-is-equiv = Finite-surjectionâ†’equiv (inc only-one) p
          Î» other â†’ inc ((other , one-right other) , refl)

  open Strategy public

  -- n-hypercubes of order m. We won't use the extra degree of generality,
  -- but it doesn't hurt.
  Hypercube : Nat â†’ Type
  Hypercube m = (Fin n â†’ Fin m) â†’ Fin m

  -- Latin hypercubes, or n-ary quasigroups.
  -- Every number appears exactly once on each "line"; equivalently,
  -- every partial application to n - 1 coordinates is an automorphism.
  is-latin : âˆ€ {m} â†’ Hypercube m â†’ Type
  is-latin {m} h = âˆ€ (i : Fin n) (xs : Fin n-1 â†’ Fin m) â†’ is-equiv Î» x â†’ h (xs [ i â‰” x ])

  Latin-hypercube : Nat â†’ Type
  Latin-hypercube m = Î£ (Hypercube m) is-latin

  -- Every latin n-hypercube h of order n induces a strategy where everyone
  -- guesses that the multiplication of all the hats is equal to their index.
  latinâ†’strategy : Latin-hypercube n â†’ Strategy
  latinâ†’strategy (h , lat) .guess i other = equivâ†’inverse (lat i other) i
  latinâ†’strategy (h , lat) .one-right hats =
    h hats , Equiv.from (eqv (h hats)) refl
    where
      module L i = Equiv (_ , lat i (delete hats i))
      eqv : âˆ€ (i : Fin n) â†’ (L.from i i â‰¡ hats i) â‰ƒ (h hats â‰¡ i)
      eqv i =
        L.from i i â‰¡ hats i â‰ƒâŸ¨ Equiv.adjunct (L.inverse i) âŸ©
        i â‰¡ L.to i (hats i) â‰ƒâŸ¨ sym-equiv âŸ©
        L.to i (hats i) â‰¡ i â‰ƒâŸ¨ âˆ™-pre-equiv (sym (ap h (funext (insert-delete hats i _ refl)))) âŸ©
        h hats â‰¡ i          â‰ƒâˆ

  -- In particular, every group structure on Fin n induces a strategy since
  -- group multiplication is an n-ary equivalence.
  groupâ†’latin : Group-on (Fin n) â†’ Latin-hypercube n
  groupâ†’latin G = mul , mul-equiv
    where
      open Group-on G hiding (magma-hlevel)

      mul : âˆ€ {m} â†’ (Fin m â†’ Fin n) â†’ Fin n
      mul xs = fold underlying-monoid (tabulate xs)

      mul-equiv : âˆ€ {m} (i : Fin (suc m)) (xs : Fin m â†’ Fin n)
                â†’ is-equiv (Î» x â†’ mul (xs [ i â‰” x ]))
      mul-equiv i xs with fin-view i
      mul-equiv _ xs | zero = â‹†-equivr _
      mul-equiv {suc m} _ xs | suc i = âˆ™-is-equiv (mul-equiv i (xs âˆ˜ fsuc)) (â‹†-equivl _)

  groupâ†’strategy : Group-on (Fin n) â†’ Strategy
  groupâ†’strategy = latinâ†’strategy âˆ˜ groupâ†’latin
